import org.adsync4j.gradle.Projects
import org.gradle.api.internal.project.ProjectInternal

import static org.adsync4j.gradle.Projects.systemTesting

Project prj = project

// in order to have implicit tasks initialized in every project
prj.rootProject.subprojects.each {
    prj.evaluationDependsOn(it.path)
}

prj.rootProject.tasks.create('taskDirs') {
    def tasksBaseDir = prj.rootProject.file('gradle/tasks')
    tasksBaseDir.deleteDir()

    prj.rootProject.allprojects.each {
        def prjTaskBaseDir = new File(tasksBaseDir, it.path.substring(1))

        // collect all tasks including implicit ones as well (like 'dependencies', 'tasks')
        def allTasks = [* it.tasks.collect(), * (it as ProjectInternal).implicitTasks.collect()]

        allTasks.each { Task task ->
            new File(prjTaskBaseDir, task.name).mkdirs()
        }
    }
}

prj.rootProject.tasks['clean'].doLast {
    prj.rootProject.file('gradle/tasks').deleteDir()
}

systemTesting.tasks['testAgainstRealActiveDirectory'].configure { Test testTask ->
    def dir = '../unversioned/systemTest'
    def adPropertiesLocation = systemTesting.file("$dir/active-directory.properties").absolutePath
    def expectedEntriesFile = systemTesting.file("$dir/expectedEntries.groovycfg").absolutePath
    testTask.jvmArgs([
            "-DadPropertiesLocation=file://$adPropertiesLocation",
            "-DexpectedEntriesFile=file://$expectedEntriesFile",
    ])
}
